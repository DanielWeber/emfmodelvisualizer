grammar org.openarchitectureware.vis.Graphmm35 with org.eclipse.xtext.common.Terminals

generate graphmm35 "http://www.openarchitectureware.org/vis/Graphmm35"

GraphCollection:
	(prolog=Prolog)?
	(graphs+=Graph)*;

Prolog:
	"prolog" "{"
		("iconbasepath" iconBasePath=STRING ";")?
	"}";

Graph:
	"graph" name=ID ("top" "=" topnode=[Node])? "{"
		("layoutHints" layoutHints = Properties )?
		"nodes" "{"
			(nodes+=Node)*
		"}"
		"edges" "{"
			(edges+=Edge)*
	  	"}"
	"}";
	  
Node:
	SimpleNode | ContainerNode | CompartmentNode | UMLNode;

SimpleNode:
	"node" name=ID 
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)?
		("icon" icon=STRING)?
		("textcolor" textcolor=ColorEnum)?
		("linecolor" linecolor=ColorEnum)?
		("fillcolor" fillcolor=ColorEnum)?
		("source" sourceLocation=STRING)? 
		("category" category=STRING)?
		("shape" shape=ShapeEnum)?
		("userData" userData = Properties )?
	";";
	
ContainerNode:
	"containernode" name=ID 
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)?
		("icon" icon=STRING)?
		("textcolor" textcolor=ColorEnum)?
		("linecolor" linecolor=ColorEnum)?
		("fillcolor" fillcolor=ColorEnum)?
		("source" sourceLocation=STRING)? 
		("category" category=STRING)?
		("shape" shape=ShapeEnum)?
		("userData" userData = Properties )?
		("contained" containedGraph=Graph )?
	";";

CompartmentNode:
	"compartmentnode" name=ID
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)?
		("icon" icon=STRING)?
		("textcolor" textcolor=ColorEnum)?
		("linecolor" linecolor=ColorEnum)?
		("fillcolor" fillcolor=ColorEnum)?
		("source" sourceLocation=STRING)? 
		("category" category=STRING)?
		("shape" shape=ShapeEnum)?
		("userData" userData = Properties )?
		(compartments+=Compartment)*
	";";

UMLNode:
	"umlnode" name=ID
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)?
		("icon" icon=STRING)?
		("textcolor" textcolor=ColorEnum)?
		("linecolor" linecolor=ColorEnum)?
		("fillcolor" fillcolor=ColorEnum)?
		("source" sourceLocation=STRING)? 
		("category" category=STRING)?
		("shape" shape=ShapeEnum)?
		("userData" userData = Properties )?
		("attributeCompartment" attributeCompartment=Compartment)
		("methodCompartment" methodCompartment=Compartment)
	";";

Compartment:
	"compartment" name=ID "{"
		("category" category=STRING)?
		("entry" entries+=STRING)*
	"}";
	
Edge:
	SimpleDirectedEdge | ComplexDirectedEdge;

SimpleDirectedEdge:
	source=[Node] direction=DirectionEnum target=[Node]
		("label" label=STRING)?
		("tooltip" tooltip=STRING)? 
		("icon" icon=STRING)?
		("color" color=ColorEnum)? 
		("weight" weight=INT)?
		("width" width=INT)?
		("style" style=LineStyleEnum)?
		("curvedepth" curvedepth=INT)?
		("category" category=STRING)?
		("userData" userData = Properties)?
	";";
	
ComplexDirectedEdge:
	source=[Node] direction=DirectionEnum target=[Node]
		("label" label=STRING)?
		("tooltip" tooltip=STRING)? 
		("icon" icon=STRING)?
		("color" color=ColorEnum)? 
		("weight" weight=INT)?
		("width" width=INT)?
		("style" style=LineStyleEnum)?
		("curvedepth" curvedepth=INT)?
		("category" category=STRING)?
		("userData" userData = Properties)?
		(containment?="containment")? (proxied?="proxied")? (inheritance?="inheritance")?
		(association=AssociationEnum "association")?
		("sourcelabelleft" sourcelabelleft=STRING)?
		("sourcelabelright" sourcelabelright=STRING)?
		("targetlabelleft" targetlabelleft=STRING)?
		("targetlabelright" targetlabelright=STRING)?
	";";
	

Properties:
	(nvpairs+=NVPair)*;


NVPair:
	name=ID ":" type=DataType "=" value=STRING ";";
	
DataType:
  	TextType | IntegerType | BooleanType;
  	
TextType:		"text";

IntegerType:	"int";

BooleanType:	"boolean";	
	
enum DirectionEnum:
	directed	= "->"	| // 1
	undirected	= "--"	| // 2
	bidirected	= "<>"	; // 3
	
enum ColorEnum:
	white		= "white"		| // 1
	lightGray	= "lightGray"	| // 2
	gray		= "gray"		| // 3
	darkGray	= "darkGray"	| // 4
	black		= "black"		| // 5
	red			= "red"			| // 6
	orange		= "orange"		| // 7
	yellow		= "yellow"		| // 8
	green		= "green"		| // 9
	lightGreen	= "lightGreen"	| // 10
	darkGreen	= "darkGreen"	| // 11
	cyan		= "cyan"		| // 12
	lightBlue	= "lightBlue"	| // 13
	blue		= "blue"		| // 14
	darkBlue	= "darkBlue"	; // 15
	

enum ShapeEnum:
	ubi_cone			= "ubi_cone"			|	// 1
	ubi_cube			= "ubi_cube"			|	// 2
	ubi_dodecahedron	= "ubi_dodecahedron"	|	// 3
	ubi_icosahedron		= "ubi_icosahedron"		|	// 4
	ubi_octahedron		= "ubi_octahedron"		|	// 5
	ubi_sphere			= "ubi_sphere"			|   // 6
	ubi_torus			= "ubi_torus"			;	// 7
	
enum LineStyleEnum:
	solid			= "solid"			|	// 1
	dashed			= "dashed"			|	// 2
	dotted			= "dotted"			|	// 3
	dashDotted		= "dashDotted"		|	// 4
	dashDotDotted	= "dashDotDotted"	;	// 5
	
enum AssociationEnum:
	generalization	= "generalization"	|	// 1
	composition		= "composition"		|	// 2
	aggregation		= "aggregation"		;	// 3

	
