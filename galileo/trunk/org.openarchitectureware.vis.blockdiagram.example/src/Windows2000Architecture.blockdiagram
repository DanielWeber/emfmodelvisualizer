// This is an example block diagram model. It refers to http://en.wikipedia.org/wiki/Block_diagram 

prolog {
	iconbasepath "platform:/resource/org.openarchitectureware.vis.blockdiagram.example/src"
}
	
diagram Windows2000Architecture  
{
	shapes toolbarLayout vertical spacing 0
	{
		default spacing 5 inset 10 fillColor white fontName "Arial" fontSize 12
		layout fillLayout horizontal mode samesize {
			space width 1; 
			block shapes fillLayout horizontal mode samesize {
				default fontColor white
				block Win32App label "Win32\nApplication" fillColor orange;
				block POSIXApp label "POSIX\nApplication" fillColor green;
				block OS2App label "OS/2\nApplication" fillColor red;
			}
		}
		space width 10;
		block userMode label "User mode" labelPlacement southwest fillColor "179,220,253" fontSize 16 fontStyle bold 
		shapes fillLayout horizontal mode samesize
		{
			block IntegralSubsystems label "Integral subsystems" labelPlacement southwest fontSize 12 fontStyle bold 
			shapes fillLayout horizontal mode proportional 
			{				
				block WorkstationService label "Workstation\nservice";
				block ServerService label "Server service";
				block Security;
			};			
			block EnvironmentSubsystems label "Environment subsystems" labelPlacement southwest fontSize 12 fontStyle bold 
			shapes fillLayout horizontal mode proportional 
			{
				block Win32;
				block POSIX;
				block OS2 label "OS/2";
			};
		};
		block kernelMode label "Kernel mode" labelPlacement southwest fillColor "85,79,157" fontSize 16 fontStyle bold fontColor white 
		shapes toolbarLayout vertical  
		{
			block Executive labelPlacement southwest fillColor lightGray fontSize 12 fontStyle bold 
			shapes toolbarLayout vertical
			{
				block ExecutiveServices;
				layout fillLayout horizontal mode proportional 
				{
					block IOManager label "I/O\nManager" source "test,1,2";
					block SecurityReferenceMonitor label "Security\nReference\nMonitor";
					block IPCManager label "IPC\nManager";
					block VMM label "Virtual\nMemory\nManager\n(VMM)";
					block ProcessManager label "Process\nManager";
					block PnPManager label "PnP\nManager";
					block PowerManager label "Power\nManager";
					block inset 2 lineWidth 1 shapes toolbarLayout vertical
					{
						block WindowManager label "Window\nManager";
						block GDI;
					};					
				};
				block ObjectManager label "Object Manager";
			};
			space width 10
			layout fillLayout horizontal mode samesize  
			{
				block KernelModeDrivers label "Kernel mode drivers";
				block Microkernel;
			};
			space width 10			
			block HAL label "Hardware Abstraction Layer (HAL)";
		};
		space width 30;
		block hardware label "Hardware" fillColor "128,0,0" fontSize 14 fontColor white;
	};
	
	connections {
		connection source Win32App target Win32 lineColor orange lineWidth 2;
		connection source POSIXApp target POSIX lineColor orange lineWidth 2;
		connection source OS2App target OS2 lineColor orange lineWidth 2;
		
		connection source Security target Win32 lineColor black lineWidth 2;
		connection source IntegralSubsystems target ExecutiveServices lineColor black lineWidth 2;
		connection source EnvironmentSubsystems target ExecutiveServices lineColor black lineWidth 2;
		connection source Executive target KernelModeDrivers lineColor white  lineWidth 2;
		connection source Executive target Microkernel lineColor white  lineWidth 2;
		connection source KernelModeDrivers target HAL lineColor white  lineWidth 2;
		connection source Microkernel target HAL lineColor white  lineWidth 2;
		connection source HAL target hardware lineColor orange lineWidth 3 
/*		labels {
			label text "East" position center alignment east;
			label text "West" position source alignment west fontName "Arial" fontSize 12 fontStyle italic;
		};*/
	
	}
}