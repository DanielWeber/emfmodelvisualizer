// specify your DSL grammar rules here ...
// IMPORTANT: You should change the property 'overwrite.pluginresources=true' in the properties file to 'overwrite.pluginresources=false' AFTER first generation

GraphCollection:
	(prolog=Prolog)?
	(graphs+=Graph)*;

Prolog:
	"prolog" "{"
		("iconbasepath" iconBasePath=STRING ";")?
	"}";

Graph:
	"graph" name=ID "{"
		"nodes" "{"
			(nodes+=Node)*
		"}"
		"edges" "{"
			(edges+=Edge)*
	  	"}"
	"}";
	  
Node:
	SimpleNode;

SimpleNode:
	name=ID 
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)?
		("icon" icon=STRING)?
		("textcolor" textcolor=ColorEnum)?
		("linecolor" linecolor=ColorEnum)?
		("fillcolor" fillcolor=ColorEnum)?
		("source" sourceLocation=STRING)? 
		("category" category=STRING)?
		(userData = UserData)?
	";";
	
Edge:
	SimpleDirectedEdge;

SimpleDirectedEdge:
	source=[Node] direction=DirectionEnum target=[Node] 
		("label" label=STRING)? 
		("tooltip" tooltip=STRING)? 
		("icon" icon=STRING)?
		("color" color=ColorEnum)? 
		("weight" weight=INT)?
		("width" width=INT)?
		("style" style=LineStyleEnum)?
		("category" category=STRING)?
		(userData = UserData)?
	";";

UserData:
	"data" "{"
		(nvpairs+=NVPair)*
	"}";


NVPair:
	name=ID ":" type=DataTypeEnum "=" value=STRING ";";
	
DataTypeEnum:
	string = "string" | integer = "int" | boolean = "boolean";	
	
Enum DirectionEnum:
	directed = "->" |
	undirected = "--";	
	
Enum ColorEnum:
	white = "white" |      
	lightGray = "lightGray" |
	gray = "gray" |      
	darkGray = "darkGray" |
	black = "black" |
	red = "red" |
	orange = "orange" |
	yellow = "yellow" |
	green = "green" |
	lightGreen = "lightGreen" |
	darkGreen = "darkGreen" |
	cyan = "cyan" |
	lightBlue = "lightBlue" |
	blue = "blue" |
	darkBlue = "darkBlue";
	
	
Enum LineStyleEnum:
	solid = "solid" | // 1
	dashed = "dashed" | // 2
	dotted = "dotted" | // 3
	dashDotted = "dashDotted" | // 4
	dashDotDotted = "dashDotDotted"; // 5
	
	